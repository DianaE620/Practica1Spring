plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.curso.diana'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
}

//JAXB
task genJaxb(group:'build',description:'Construye clases jaxb') {
	ext.sourcesDir = "src/generated/jaxb"
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "src/main/resources/wsdl/WSProductoContrato.wsdl"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
				package: 'com.curso.diana.practica1.dto') {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}

task afterEclipseImport {
	dependsOn "genJaxb"
}

configurations {
	jaxb
}
//END JAXB

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
	
	//MYBATIS
	implementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '2.1.0'
	implementation group: 'com.h2database', name: 'h2', version: '1.4.199'
	
	//JAXB
	jaxb("org.glassfish.jaxb:jaxb-xjc:2.2.11")
	
	//SOAP
	implementation("org.springframework.boot:spring-boot-starter-web-services")
	implementation("wsdl4j:wsdl4j:1.6.1")
	implementation(files(genJaxb.classesDir).builtBy(genJaxb))

}
